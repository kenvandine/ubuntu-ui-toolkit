# sets projectwide variables and implicitely
# enables qmake to lookup the projectroot/features
# directory for possible feature files
#
# Any variable specified here, must be set manually
# inside the debian/rules file

load(qt_build_config)
CONFIG += warning_clean

MODULE_VERSION = 5.5.0

ROOT_SOURCE_DIR=$$PWD
ROOT_BUILD_DIR=$$shadowed($$PWD)

exists($$PWD/../qt.pro) {
    CONFIG += build_with_qt
}

defineReplace(readVarByName) {
    eval("_value=\$\$$${1}")
    return($$_value)
}

defineTest(qmlRegisterType) {
    varName = $${1}_$${2}_$${3}_qmltype_desc
    varName = $$replace(varName,"[^A-Za-z0-9]",_)_fileonly_qmltype_desc

    $${varName}.typeName = $$1
    export($${varName}.typeName)

    $${varName}.major    = $$2
    export($${varName}.major)

    $${varName}.minor    = $$3
    export($${varName}.minor)

    $${varName}.relPath  = $$4
    export($${varName}.relPath)

    UBUNTU_QML_COMPONENTS += $${varName}
    export(UBUNTU_QML_COMPONENTS)
}

defineTest(qmlRegisterJSType) {
    varName = $${1}_$${2}_$${3}_qmltype_desc
    varName = $$replace(varName,"[^A-Za-z0-9]",_)_fileonly_qmltype_desc

    $${varName}.typeName = $$1
    export($${varName}.typeName)

    $${varName}.major    = $$2
    export($${varName}.major)

    $${varName}.minor    = $$3
    export($${varName}.minor)

    $${varName}.relPath  = $$4
    export($${varName}.relPath)

    $${varName}.jsType  = true
    export($${varName}.jsType)

    UBUNTU_QML_COMPONENTS += $${varName}
    export(UBUNTU_QML_COMPONENTS)
}

defineTest(qmlRegisterInternalType) {
    varName = $${1}_internal_qmltype_desc
    varName = $$replace(varName,"[^A-Za-z0-9]",_)_fileonly_qmltype_desc

    $${varName}.typeName = $$1
    export($${varName}.typeName)

    $${varName}.relPath  = $$2
    export($${varName}.relPath)

    $${varName}.internal = true
    export($${varName}.internal)

    UBUNTU_QML_COMPONENTS += $${varName}
    export(UBUNTU_QML_COMPONENTS)
}

defineTest(qmlRegisterSingleton) {
    varName = $${1}_$${2}_$${3}_singleton_qmltype_desc
    varName = $$replace(varName,"[^A-Za-z0-9]",_)_fileonly_qmltype_desc

    $${varName}.typeName = $$1
    export($${varName}.typeName)

    $${varName}.major    = $$2
    export($${varName}.major)

    $${varName}.minor    = $$3
    export($${varName}.minor)

    $${varName}.relPath  = $$4
    export($${varName}.relPath)

    $${varName}.singleton  = true
    export($${varName}.singleton)

    UBUNTU_QML_COMPONENTS += $${varName}
    export(UBUNTU_QML_COMPONENTS)
}

defineTest(qmlRegisterFile) {
    varName = $$replace(1,"[^A-Za-z0-9]",_)_fileonly_qmltype_desc

    $${varName}.relPath  = $$1
    export($${varName}.relPath)
    $${varName}.fileOnly = true
    export($${varName}.fileOnly)

    UBUNTU_QML_COMPONENTS += $${varName}
    export(UBUNTU_QML_COMPONENTS)
}
